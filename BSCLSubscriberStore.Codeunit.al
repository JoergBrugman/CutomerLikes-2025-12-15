codeunit 50200 "BSCL Subscriber Store"
{
    var
        OnBeforeDeleteBookErr: Label 'You are not allowed to delete %1 %2 because it is liked by one or more customer';

    [EventSubscriber(ObjectType::Table, Database::"BSB Book", OnBeforeOnDelete, '', false, false)]
    local procedure "BSB Book_OnBeforeOnDelete"(var Rec: Record "BSB Book"; var IsHandled: Boolean)
    var
        Customer: Record Customer;
    begin
        if IsHandled then
            exit;
        Customer.SetCurrentKey("BSB Favorite Book No.");
        Customer.SetRange("BSB Favorite Book No.", Rec."No.");
        if not Customer.IsEmpty then
            Error(OnBeforeDeleteBookErr, Rec.TableCaption, Rec."No.");
        IsHandled := true;
    end;

    [EventSubscriber(ObjectType::Page, Page::"BSB Book List", OnBeforeProcessBookType, '', false, false)]
    local procedure "BSB Book List_OnBeforeProcessBookType"(Rec: Record "BSB Book"; var IsHandled: Boolean)
    var
        BSCLBookTypeeBookImpl: Codeunit "BSCL Book Type eBook Impl.";
    begin
        if Rec.Type = "BSB Book Type"::eBook then begin
            BSCLBookTypeeBookImpl.StartDeployBook();
            BSCLBookTypeeBookImpl.StartDeliverBook();
            IsHandled := true;
        end
    end;

}